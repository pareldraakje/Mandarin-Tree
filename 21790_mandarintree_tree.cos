**
** This is a tree. It grows lovely mandarines!
** Graphics by Linda
** Coding by Pilla

** Enjoy!

**
** CLASSIFIERS
**

** 2 4 21790 - The small mandarin tree
** 2 7 21790 - The mandarin flower
** 2 8 21790 - The mandarin fruit
** 2 10 21790 - The mandarin detritus

**
** Variables
**

** ov00 - max life
** ov01 - current life
** ov02 - Mirroring
** ov03 - State of flower: 0 - not set, 1 - flowering, 2 - fruiting. Defaults to fruiting.
** ov05 - Tree attached to (for flowers and fruit)
** ov06 - first flower spot posx
** ov07 - first flower spot posy
** ov08 - second flower spot posx
** ov09 - second flower spot posy
** ov10 - third flower spot posx
** ov11 - third flower spot posy

**
** CAOS2PRAY CHUNK COMES HERE
**


** INJECTION SCRIPT
**

targ pntr
setv va00 posx
setv va01 posy
new: simp 2 4 21790 "mandarintree_tree_linda" 16 0 rand 1000 2000
setv va02 wdth
divv va02 2
setv va03 hght
divv va03 2
subv va00 va02
subv va01 va03
mvto va00 va01


** INSTALL SCRIPTS
**
** This script creates a tree.

scrp 2 4 21790 10
	inst
* Attributes: Can be activated (4). If there's fruit, it drops.
* Can be picked up by the mouse (2).
* It also suffers collisions (64) and physics (128).
	attr 198
* Behaviour: Can be pushed (1) and hit (8). 
* Hitting the plant can make it drop fruit.
* It also suffers collisions (64).
	bhvr 73
* Set the permeability to 100, it won't fall through anything.
	perm 100
* Physics: Heavy
	accg 5
	elas 0
	fric 100
	aero 1
* Set the life variables: maximum lifespan and current life.
	setv ov00 rand 60 90
	setv ov01 0
* Set the tree mirroring
	setv ov02 rand 0 1
* Set the tree range, it will search in this range for attached flowers
	rnge 200
* Set the initial tick - 35 seconds
* This will reset to a smaller value once the tree is fully grown.
	tick 700

* Set the base leaf graphics to use according to the season
	doif sean eq 0
		base 0
	elif sean eq 1
		base 4
	elif sean eq 2
		base 8
	else
		base 12
	endi
	pose 0

* Set the handle for the adult tree (not needed I think, maybe for gardenbox?)
	puhl -1 91 60

* Randomly mirror the tree
	mira ov02
	slow
endm

** This script creates fruit.
scrp 2 8 21790 10
	inst
* Attributes: Can be picked up by the mouse (2).
* It also suffers collisions (64) and physics (128).
	attr 194
* Behaviour: can be eaten (16) and picked up (32).
	bhvr 48
* Permiability: Set to 90, shouldn't be dropping through many floors.
	perm 90
* Physics: Normal physics I guess
	accg 1.9
	elas 40
	fric 85
	aero 1
* Set the life variables: maximum and current life.
	setv ov00 rand 4 8
	setv ov01 0
	seta ov05 null
* Set the tick - 15 seconds
	tick 300
	slow
endm

** This script creates a flower.
scrp 2 7 21790 10
	inst
* Attributes: It's floatable (32). Physics will be added later, when it drops from the tree.
	attr 32
* Behaviour: can be eaten (16) and picked up (32).
	bhvr 48
* Permiability: Set to 90, shouldn't be dropping through many floors.
	perm 90
* Set the life variables: maximum and current life.
	setv ov00 rand 12 15
	setv ov01 0
* Set the tick - 15 to 25 seconds
	tick rand 300 500
	setv ov02 rand 0 1
	mira ov02
	slow
endm

**
** TIMER SCRIPTS
**

** The tree timer script
** This script makes the tree grow and spawn fruit.
scrp 2 4 21790 9

* Check if the season base is correct
	setv va00 pose
	doif sean eq 0
		base 0
* Set the tick to a random value between 40 and 55 seconds
		tick rand 800 1100
	elif sean eq 1
		base 4
* Set the tick to a random value between 30 and 45 seconds
		tick rand 600 900
	elif sean eq 2
		base 8
* Set the tick to a random value between 20 and 45 seconds
		tick rand 400 900
	else
		base 12
* Set the tick to a random value between 50 and 90 seconds
		tick rand 1000 1800
	endi
	pose va00
* Check if the tree is still growing
	doif pose lt 3
* If it is, grow! <3
		setv va00 pose
		addv va00 1
		pose va00
		mira ov02
	else
* If the tree is not growing, spawn some fruit!
* First count the flowers already present
		targ ownr
		seta va04 ownr
		setv va00 0
		inst
		esee 2 7 21790
			doif targ ne null
				doif ov05 eq va04
					addv va00 1
				endi
			endi
		next
		slow
* If there are less than 3 flowers around, create a new flower on the tree.
		doif va00 lt 3

			targ ownr
* spot where flower will appear: va00 posx va01 posy
			setv va01 35
			addv va01 rand 0 73
			setv va02 50
			addv va02 rand 0 38
* plane
			setv va05 plne
			addv va05 rand 10 50

* Create flower
			new: simp 2 7 21790 "mandarintree_flower_linda" 5 0 va05
			seta ov05 va04
			frel va04
			flto va01 va02
		endi
	endi
	targ ownr
* Increase the current life variable by one
	addv ov01 1
* If life's up, die
	doif ov01 ge ov00
		kill ownr
	endi
endm

** The fruit timer script
scrp 2 8 21790 9
* Do not trigger when carried
	doif carr eq null
* Age the fruit
		addv ov01 1
* If life's up, die.
		doif ov01 ge ov00
			kill ownr
		endi
	endi
endm

** The flower timer script
scrp 2 7 21790 9
	tick rand 700 1100
* Age the flower
	addv ov01 1
* Check if the flower is going to be fruiting or flowering
	doif ov03 eq 0
		setv va01 0
* If ov03 isn't set, let whether it fruits depend on the season.
* If ov03 is 1, the flower will fall off as a flower.
* If ov03 is 2, the flower will grow until it becomes fruit.
* The chance of a flower growing into fruit is 3/4 in summer and fall, 2/4 in spring and 1 in winter.
		doif sean eq 0
			setv va00 rand 1 4
			doif va00 gt 1
				setv va01 1
			endi
		elif sean eq 4
			setv va01 1
		else
			setv va00 rand 1 4
			doif va00 gt 2
				setv va01 1
			endi
		endi
		doif va01 eq 1
			setv ov03 2
		else
			setv ov03 1
		endi
	endi

* If the life force is up, check if you're hanging on a tree.
* If you're not hanging on a tree, die
	doif ov05 eq null
		doif ov01 ge ov00
			kill ownr
			stop
		endi
	endi

* If the flower isn't fully grown yet, grow
	doif pose lt 1
		doif sean eq 3
			pose 3
			setv ov03 2
		else
			setv va00 pose
			addv va00 1
			pose va00
			mira ov02
		endi
	else
* If flowering
* If you belong to a tree, either grow into fruit or fall off.
* Case: The flower falls off without turning into fruit
		doif ov03 eq 1
			doif pose lt 2
				setv va00 pose
				addv va00 1
				pose va00
				mira ov02
			else
* First set physics so it'll start falling
				pose 2
				attr 194
				accg 0.5
				fric 95
				elas 0
				aero 5
* Reset life
				setv ov01 0
* Disconnect from the tree
				seta ov05 null
				doif tmvt posx posy ne 0
					mvsf posx posy
				else
					kill targ
				endi
			endi
* Case: the flower turns into fruit
		else
			doif pose lt 4
				setv va00 pose
				addv va00 1
				pose va00
			else
* Get the xpos ypos coordinates for the new fruit + plane
				targ ownr
				setv va02 posl
				setv va03 post
				setv va04 plne
				addv va04 1
* Create a new fruit
				new: simp 2 8 21790 "mandarintree_fruit_linda" 4 0 va04
* Move the fruit in place if possible
				doif tmvt va02 va03 eq 1
					mvto va02 va03
				else
					kill targ
				endi
* Kill the flower
				kill ownr
			endi
		endi
	endi
endm


** Push script

scrp 2 4 21790 1
	targ pntr
	anim [4 5 5 0]
	targ ownr
	velo 0 -5
** All full grown flowers will be dropped.
	inst
	seta va00 targ
	esee 2 7 21790
		doif targ ne null
			doif ov05 eq va00
				doif pose eq 2
					setv ov03 1

* First set physics so it'll start falling
					attr 194
					accg 0.5
					fric 95
					elas 0
					aero 5
* Reset life
					setv ov01 0
* Disconnect from the tree
					seta ov05 null
				endi
			endi
		endi
	next
	slow
endm

** Hit script
scrp 2 4 21790 3
endm

** Pickup script
scrp 2 4 21790 4
** All full grown flowers will be dropped, and ungrown flowers will be killed.
	inst
	seta va00 targ
	esee 2 7 21790
		doif targ ne null
			doif ov05 eq va00
				doif pose eq 2
* First set physics so it'll start falling
					setv ov03 1
					attr 194
					accg 0.5
					fric 95
					elas 0
					aero 5
* Reset life
					setv ov01 0
* Disconnect from the tree
					seta ov05 null
				else
					kill targ
				endi
			endi
		endi
	next
	slow
endm

** Eat script
scrp 2 7 21790 12
endm
scrp 2 8 21790 12
endm




**
** Removal script
**

rscr

enum 2 4 21790
	kill targ
next
enum 2 7 21790
	kill targ
next
enum 2 8 21790
	kill targ
next